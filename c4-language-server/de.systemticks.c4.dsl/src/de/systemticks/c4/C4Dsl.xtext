grammar de.systemticks.c4.C4Dsl with org.eclipse.xtext.common.Terminals

generate c4Dsl "http://www.systemticks.de/c4/C4Dsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

Workspace:
	{Workspace} 'workspace' name=STRING description=STRING? '{'
		model = Model
		viewSection = ViewSection?
	'}'
;	

// Model

Model:		
	{Model} 'model'  '{'
		(enterprise=Enterprise? &		
		(person+=Person | 
			softwareSystems+=SoftwareSystem |
			relationShips+=RelationShip
		)*)	
	'}'
;

NamedElement:
	Person | SoftwareSystem | Container | Component
;

Enterprise:
	'enterprise' label=STRING '{'
		(	person+=Person | 
			softwareSystems+=SoftwareSystem |
			relationShips+=RelationShip
		)*	
	'}'
;

//[<identifier> = ]person <name> [description] [tags]
Person:
	(name=ID '=')? 'person' label=STRING (description=STRING | description=STRING taglist=STRING)? 
;

Component:
	(name=ID '=')? 'component' label=STRING 
		(description=STRING | description=STRING technology=STRING | description=STRING technology=STRING taglist=STRING)? 	
;

Container:
	(name=ID '=')? 'container' label=STRING
		(description=STRING | description=STRING technology=STRING | description=STRING technology=STRING taglist=STRING)? 	
	('{'
		components+=Component+
	'}')?
;

SoftwareSystem:
	(name=ID '=')? ('softwareSystem'|'softwaresystem') label=STRING (description=STRING | description=STRING taglist=STRING)?  
	('{'
		container+=Container+
	'}')?
;

RelationShip:
	from=[NamedElement] "->" to=[NamedElement] 
		(description=STRING | description=STRING technology=STRING | description=STRING technology=STRING taglist=STRING)? 	
;

// Views
ViewSection:
	{ViewSection} 'views' '{'
		(views += View |
		styles += Styles )*
	'}'
;

View:
	SystemLandscape | SystemContextView | ContainerView | ComponentView
;

SystemLandscape:
	('systemLandscape'|'systemlandscape') key=STRING description=STRING? '{'
		content=CustomizedContent
	'}'
;

SystemContextView:
	('systemContext'|'systemcontext') system=[SoftwareSystem] key=STRING description=STRING? '{'
		content=CustomizedContent
	'}'
;

ContainerView:
	'container' system=[SoftwareSystem] key=STRING description=STRING? '{'
		content=CustomizedContent
	'}' 
;

ComponentView:
	'component' container=[Container] key=STRING description=STRING? '{'
		content=CustomizedContent
	'}' 
;

CustomizedContent:	 
		{CustomizedContent} (
			includes += Include* &
	 		excludes += Exclude* &
	 		autoLayout = AutoLayout? &
	 		animationStep += AnimationStep* &
	 		('title' title = STRING)?
	 	)	
;

AutoLayout:
	{AutoLayout} 'autoLayout' direction=LayoutDirection? (rankSeperation=INT | rankSeperation=INT nodeSeperation=INT)?
;

enum LayoutDirection:
	tb|bt|lr|rl;

Include:
	'include' (all?='*' | elements+=[NamedElement]+)	
;

Exclude:
	'exclude' elements+=[NamedElement]+	
;

AnimationStep:
	'animationStep' elements+=[NamedElement]+	
;


Styles:
	{Styles} 'styles' '{'
		(styledElements+=StyledElement |
		styledRelationShips+=StyledRelationShip)*
	'}'
;

StyledElement:
	'element' tag=STRING '{'
		(
			('shape' shape=StyleShape)? &
			('icon' file=STRING)? &
			('width' width=INT)? &
			('height' height=INT)? &			 
			('background' backgroundColor=HEX_DECIMAL)? &
			('color' color=HEX_DECIMAL)? &
			('colour' colour=HEX_DECIMAL)? & 
			('stroke' stroke=HEX_DECIMAL)? &
			('fontSize' fontSize=INT)? & 
			('border' border=("solid"|"dashed"|"dotted"))? &
			('opacity' opacity=INT)? &
			('metadata' metadata=("true"|"false"))? & 
			('description' description=("true"|"false"))?		
		)  
	'}'
;

StyledRelationShip:
	'relationship' tag=STRING '{'
		(
			('thickness' thickness=INT)? &
			('color' color=HEX_DECIMAL)? &
			('colour' colour=HEX_DECIMAL)? & 
			('dashed' dashed=("true"|"false"))? &
			('routing' routing=("Direct"|"Orthogonal"|"Curved"))? &
			('fontSize' fontSize=INT)? &
			('width' width=INT)? &
			('position' position=INT)? &
			('opacity' opacity=INT)?						 
		)
	'}'
;

enum StyleShape:	
	Box|RoundedBox|Circle|Ellipse|Hexagon|Cylinder|Pipe|Person|Robot|Folder|WebBrowser|MobileDevicePortrait|MobileDeviceLandscape|Component	;


terminal HEX_DECIMAL: '#' (('0'..'9') | ('A' .. 'F') | ('a' .. 'f') )+;

@Override 
terminal SL_COMMENT : ('# '|'//') !('\n'|'\r')* ('\r'? '\n')?;


